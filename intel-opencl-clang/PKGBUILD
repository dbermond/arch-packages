# Maintainer: Daniel Bermond < gmail-com: danielbermond >

_srcname=opencl-clang
pkgname=intel-${_srcname}
pkgver=8.0.0
pkgrel=2
pkgdesc="A wrapper library around clang that can compile OpenCL C kernels to SPIR-V modules"
arch=(x86_64)
url="https://github.com/intel/opencl-clang/"
license=(custom)
depends=(clang llvm-libs)
makedepends=(cmake llvm spirv-llvm-translator git)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=(da042ff8c7e2db5bf58b01dcb620b37abe155980521d97c41698e62fa31b9547)

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${_srcname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLLVMSPIRV_INCLUDED_IN_LLVM=OFF \
        -DSPIRV_TRANSLATOR_DIR=/usr \
        -DLLVM_NO_DEAD_STRIP=ON \
        -Wno-dev
    make
}

package() {
    cd build
    make DESTDIR="$pkgdir" install
    
    # Provide libopencl_clang.so
    local _sover
    _sover="$(find -L "${pkgdir}" -type f -name 'libcommon_clang.so.*' | head -n1)"
    _sover="${_sover##*.}"
    ln -s libcommon_clang.so "${pkgdir}"/usr/lib/libopencl_clang.so
    ln -s libcommon_clang.so.${_sover} "${pkgdir}"/usr/lib/libopencl_clang.so.${_sover}
    
    install -Dm644 ../${_srcname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
